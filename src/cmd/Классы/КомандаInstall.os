#Использовать fs

///////////////////////////////////////////////////////////////////////////////
//
// Служебный модуль с реализацией работы команды
//
///////////////////////////////////////////////////////////////////////////////

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Аргумент("PATH", "" ,"Путь к каталогу установки git-hook разбора правил (По умолчанию текущий каталог)")
		.ТСтрока()
		.ПоУмолчанию(ТекущийКаталог())
		.Обязательный(Ложь); // тип опции Строка
		
КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	ПутьКУстановки = Команда.ЗначениеАргумента("PATH");
	
	КаталогПроекта = Новый Файл(ОбъединитьПути(ПутьКУстановки, ".git"));
	Если Не КаталогПроекта.Существует() Тогда
		СообщениеОшибки = СтрШаблон("Каталог <%1> не является репозиторием GIT", ПутьКУстановки);
		ВызватьИсключение Новый ИнформацияОбОшибке(СообщениеОшибки, "Завершение работы");
	КонецЕсли;

	Попытка
		МассивФайлов = НайтиФайлы(ПараметрыПриложения.ПолучитьКаталогУтилитДляУстановки() + ПолучитьРазделительПути(), "*");
		Для Каждого Файл Из МассивФайлов Цикл
			КопироватьФайл(Файл.ПолноеИмя, ОбъединитьПути(КаталогПроекта.ПолноеИмя, "hooks", Файл.Имя));
		КонецЦикла;
		Сообщить("Установка завершена");
	Исключение
		ВызватьИсключение ("Не удалось установить gitrules по причине: " + ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры // ВыполнитьКоманду
