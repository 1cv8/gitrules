#Использовать fs

///////////////////////////////////////////////////////////////////////////////
//
// Служебный модуль с реализацией работы команды
//
///////////////////////////////////////////////////////////////////////////////

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Аргумент("FILE", "" ,"Путь к файлу правилу xml. Например /path/to/file/ExchangeRules.xml")
        .ТСтрока(); // тип опции Строка

	Команда.Аргумент("PATH", "" ,"Путь к каталогу распаковки правил")
		.ТСтрока(); // тип опции Строка
		
	Команда.Опция("h here", Ложь, "Выгружать правило в выбранный каталог")
		.ТБулево()
		.Обязательный(Ложь);
		
КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	ПутьКФайлуПравила = Команда.ЗначениеАргумента("FILE");
	КаталогРаспаковки = Команда.ЗначениеАргумента("PATH");
	ВыгружатьВВыбранныйКаталог = Команда.ЗначениеОпции("here");
	
	ФайлСПравилами = Новый Файл(ПутьКФайлуПравила);
	Если Не ФайлСПравилами.Существует() Или ФайлСПравилами.ЭтоКаталог() Тогда
		
		СтрокаОшибки = СтрШаблон("Файл с правилами обмена <%1> не существует", ПутьКФайлуПравила);
		ВызватьИсключение СтрокаОшибки;

	КонецЕсли;

	Если Не ФайлСПравилами.Расширение = ".xml" Тогда
		СтрокаОшибки = СтрШаблон("Расширение файла <%1> не xml", ПутьКФайлуПравила);
		ВызватьИсключение СтрокаОшибки;
	КонецЕсли;

	ФС.ОбеспечитьКаталог(КаталогРаспаковки);

	МенеджерПравил = Новый ОбработкаПравил();
	МенеджерПравил.УстановитьЗначениеВыгружатьВВыбранныйКаталог(ВыгружатьВВыбранныйКаталог);
	МенеджерПравил.РазложитьПравилаКонвертации(ПутьКФайлуПравила, КаталогРаспаковки);

КонецПроцедуры
