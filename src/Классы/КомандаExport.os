///////////////////////////////////////////////////////////////////////////////
//
// Служебный модуль с реализацией работы команды
//
///////////////////////////////////////////////////////////////////////////////

Перем Log;

Процедура НастроитьКоманду(Знач Команда, Знач Парсер) Экспорт

	// Добавление параметров команды
	Парсер.ДобавитьПозиционныйПараметрКоманды(Команда, "ПутьКФайлуПравила", "Путь к файлу правилу xml. Например /path/to/file/ExchangeRules.xml");
	Парсер.ДобавитьПозиционныйПараметрКоманды(Команда, "КаталогРаспаковки", "Путь к каталогу распаковки правил");

КонецПроцедуры // НастроитьКоманду

// Выполняет логику команды
// 
// Параметры:
//   ПараметрыКоманды - Соответствие - Соответствие ключей командной строки и их значений
//   Приложение - Модуль - Модуль менеджера приложения
//
Функция ВыполнитьКоманду(Знач ПараметрыКоманды, Знач Приложение) Экспорт
	
	Log = Приложение.ПолучитьЛог();
	ПутьКФайлуПравила = ПараметрыКоманды["ПутьКФайлуПравила"];
	ФайлСПравилами = Новый Файл(ПутьКФайлуПравила);
	Если Не ФайлСПравилами.Существует() Или ФайлСПравилами.ЭтоКаталог() Тогда
		Log.Ошибка("Файл с правилами обмена " + ПутьКФайлуПравила + " не существует");
		Возврат Приложение.РезультатыКоманд().НеверныеПараметры;
	КонецЕсли;

	Если Не ФайлСПравилами.Расширение = ".xml" Тогда
		Log.Ошибка("Расширение файла не xml");
		Возврат Приложение.РезультатыКоманд().НеверныеПараметры;
	КонецЕсли;

	КаталогРаспаковки = ПараметрыКоманды["КаталогРаспаковки"];
	ФайлКаталог = Новый Файл(КаталогРаспаковки);
	Если Не ФайлКаталог.Существует() Или ФайлКаталог.ЭтоФайл() Тогда
		Log.Ошибка("Каталог распаковки " + КаталогРаспаковки + " не существует");
		Возврат Приложение.РезультатыКоманд().НеверныеПараметры;
	КонецЕсли;

	МенеджерПравил = Новый ОбработкаПравил();
	МенеджерПравил.РазложитьПравилаКонвертации(ПутьКФайлуПравила, КаталогРаспаковки);

	Возврат Приложение.РезультатыКоманд().Успех;
	
КонецФункции // ВыполнитьКоманду
